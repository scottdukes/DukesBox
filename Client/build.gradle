import net.scottdukes.gradle.plugins.JavaAndroidPlugin

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

ext {
    minSdkVersion = 14
    compileSdkVersion = 19
    buildToolsVersion = '19.1.0'
}

apply from: "$rootDir/gradle/dependencies.gradle"

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

subprojects {
    // default group/organisation for all build outputs
    group = 'net.scottdukes'

    // project version
    version = '1.0'

    plugins.withType(com.android.build.gradle.BasePlugin) {
        project.apply from: "$rootDir/gradle/androidProjectDefaults.gradle"
    }
    plugins.withType(JavaPlugin) {
        project.apply from: "$rootDir/gradle/providedConfiguration.gradle"
    }
    plugins.withType(JavaAndroidPlugin) {
        android {
            compileSdkVersion project.compileSdkVersion
        }
    }
}